plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
}

apply from: 'artifacts-android.gradle'
apply from: 'publish-maven.gradle'
apply from: 'copy-dependencies.gradle'
apply from: 'codecov.gradle'
apply from: 'dokka.gradle'

ext.room_version = '2.4.3'
ext.compose_version = '1.4.3'
ext.compose_compiler_version = '1.4.7'
ext.accompanist_version = '0.30.1'
ext.koin_version = '3.1.6'
ext.coil_version = '2.2.2'
ext.moshi_version = '1.14.0'

android {
    compileSdk 33

    namespace "com.appcues"

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testApplicationId = "com.appcues.test"

        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    resourcePrefix 'appcues_'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn", "-Xexplicit-api=strict"]
    }

    buildFeatures {
        buildConfig true
        compose true
        viewBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "$compose_compiler_version"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Androidx
    implementation "androidx.core:core-ktx:1.9.0"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.browser:browser:1.4.0"
    implementation "androidx.startup:startup-runtime:1.1.1"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    // Workaround for an issue that will be fixed by google some day.
    // https://issuetracker.google.com/issues/227767363
    debugImplementation "androidx.customview:customview:1.2.0-alpha02"
    debugImplementation "androidx.customview:customview-poolingcontainer:1.0.0"
    // ----
    implementation "com.google.android.material:material:1.7.0"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.savedstate:savedstate:1.2.0"
    // Image Loader
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"
    // RESTApi
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    // Dependency Injection
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"
    // View Pager
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    // Webview
    implementation "com.google.accompanist:accompanist-webview:$accompanist_version"
    // Navigation Animated
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    // Play In-App Review
    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.play:review-ktx:2.0.1'

    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:1.12.2"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.10.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation "io.insert-koin:koin-test-junit4:3.1.4"
}
