import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'kotlin-kapt'
    id "com.vanniktech.maven.publish" version "0.32.0"
}

apply from: 'artifacts-android.gradle'
apply from: 'copy-dependencies.gradle'
apply from: 'dokka.gradle'
apply from: 'appcues-properties.gradle'

ext.room_version = '2.6.1'
ext.compose_version = '1.7.3'
ext.compose_compiler_version = '1.4.8'
ext.coil_version = '2.4.0'
ext.moshi_version = '1.15.0'

android {
    compileSdk 34

    namespace "com.appcues"

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testApplicationId = "com.appcues.test"

        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "String", "SDK_VERSION", "\"${VERSION_NAME}\""
    }

    resourcePrefix 'appcues_'

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn", "-Xexplicit-api=strict"]
    }

    buildFeatures {
        buildConfig true
        compose true
        viewBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "$compose_compiler_version"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Androidx
    implementation "androidx.core:core-ktx:1.13.1"
    implementation 'androidx.activity:activity-compose:1.9.2'
    implementation "androidx.browser:browser:1.8.0"
    implementation "androidx.startup:startup-runtime:1.2.0"
    implementation "androidx.navigation:navigation-compose:2.7.3"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    implementation "com.google.android.material:material:1.12.0"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.8.6'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.savedstate:savedstate:1.2.1"
    // Image Loader
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"
    // RESTApi
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:4.11.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.11.0"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    // Play In-App Review
    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.play:review-ktx:2.0.1'
    // Messaging
    implementation "com.google.firebase:firebase-messaging-ktx:24.0.2"
    // Animation
    implementation 'nl.dionsegijn:konfetti-compose:2.0.4'

    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:1.13.5"
    testImplementation "com.google.truth:truth:1.1.5"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.11.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1"
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'app.cash.turbine:turbine:1.0.0'
}

mavenPublishing {
    coordinates(GROUP_ID, ARTIFACT_ID, VERSION_NAME)
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, false)
    signAllPublications()

    pom {
        name = NAME
        description = DESCRIPTION
        url = ORG_URL
        licenses {
            license {
                name = LICENSE
                url = LICENSE_URL
            }
        }
        developers {
            developer {
                id = ORG_ID
                name = ORG_NAME
                organization = ORG_NAME
                organizationUrl = ORG_URL
                email = ORG_EMAIL
            }
        }
        scm {
            connection = 'scm:git:git://github.com/' + GITHUB_PATH + '.git'
            developerConnection = 'scm:git:ssh://github.com:' + GITHUB_PATH + '.git'
            url = 'https://github.com/' + GITHUB_PATH
        }
    }
}
