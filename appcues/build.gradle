plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'shot'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
}

apply from: 'publish-maven.gradle'
apply from: 'copy-dependencies.gradle'
apply from: 'codecov.gradle'
apply from: 'dokka.gradle'

ext.room_version = '2.4.2'
ext.compose_version = '1.2.0-rc03'
ext.compose_compiler_version = '1.2.0'
ext.accompanist_version = '0.24.13-rc'

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "com.karumi.shot.ShotTestRunner"
        testApplicationId = "com.appcues.test"

        consumerProguardFiles "consumer-rules.pro"
    }

    resourcePrefix 'appcues_'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "$compose_compiler_version"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    shot {
        applicationId = "com.appcues"
    }

}

dependencies {
    // Androidx
    implementation "androidx.core:core-ktx:1.8.0"
    implementation 'androidx.activity:activity-compose:1.5.0'
    implementation "androidx.browser:browser:1.4.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.navigation:navigation-compose:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    // Workaround for an issue that will be fixed by google some day.
    // https://issuetracker.google.com/issues/227767363
    debugImplementation "androidx.customview:customview:1.2.0-alpha01"
    debugImplementation "androidx.customview:customview-poolingcontainer:1.0.0-rc01"
    // ----
    implementation "com.google.android.material:material:1.6.1"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // Line below is a temporary workaround for an issue that will be fixed in Room 2.5.0 - since
    // Room 2.4.2 has a compatibility issue with Kotlin 1.7.0
    // https://issuetracker.google.com/issues/236612358
    kapt "org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.5.0"
    // Image Loader
    implementation "io.coil-kt:coil-compose:2.1.0"
    implementation "io.coil-kt:coil-gif:2.1.0"
    implementation "io.coil-kt:coil-svg:2.1.0"
    // RESTApi
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
    implementation 'com.squareup.moshi:moshi:1.13.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.13.0'
    // Dependency Injection
    implementation "io.insert-koin:koin-core:3.1.4"
    implementation "io.insert-koin:koin-android:3.1.4"
    // View Pager
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    // Webview
    implementation "com.google.accompanist:accompanist-webview:$accompanist_version"
    // Navigation Animated
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    // Play In-App Review
    implementation 'com.google.android.play:review:2.0.0'
    implementation 'com.google.android.play:review-ktx:2.0.0'

    testImplementation "junit:junit:4.13.2"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.9.3"
    testImplementation "io.mockk:mockk:1.12.2"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1"
    testImplementation "io.insert-koin:koin-test-junit4:3.1.4"

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.1.1"
}
