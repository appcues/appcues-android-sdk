apply plugin: 'maven-publish'

def appcuesProperties = new Properties()
file("appcues.properties").withInputStream { appcuesProperties.load(it) }

android {
    defaultConfig {
        buildConfigField "String", "SDK_VERSION", "\"${appcuesProperties.getProperty("VERSION")}\""
    }
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = appcuesProperties.getProperty("GROUP_ID")
                artifactId = appcuesProperties.getProperty("NAME")
                version = appcuesProperties.getProperty("VERSION")
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = appcuesProperties.getProperty("GROUP_ID")
                artifactId = appcuesProperties.getProperty("NAME") + "-debug"
                version = appcuesProperties.getProperty("VERSION")
            }
        }
    }
}
