plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
}

android {
    compileSdk 34

    namespace "com.appcues.samples.kotlin"

    signingConfigs {
        release {
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
            storeFile file('../../appcues.keystore')
            storePassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    defaultConfig {
        applicationId "com.appcues.samples.kotlin"
        minSdk 21
        targetSdk 34
        versionCode Integer.valueOf(System.getenv("CIRCLE_BUILD_NUM") ?: 3)
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "APPCUES_ACCOUNT_ID", "\"17411\""
        buildConfigField "String", "APPCUES_APPLICATION_ID", "\"af99603f-2714-4055-8d96-ae5bef31a94d\""
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }
}

dependencies {
    implementation project(":appcues")

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.3'

    implementation platform('com.google.firebase:firebase-bom:32.6.0')
    implementation 'com.google.firebase:firebase-messaging-ktx:23.3.1'
}

tasks.register('versionTxt') {
    doLast {
        new File(projectDir.parentFile.parentFile, "sample_version.txt").text = "$android.defaultConfig.versionCode ($android.defaultConfig.versionName)"
    }
}
