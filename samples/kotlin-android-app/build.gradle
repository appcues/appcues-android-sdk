plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 31

    signingConfigs {
        release {
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
            storeFile file('../../appcues.keystore')
            storePassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    defaultConfig {
        applicationId "com.appcues.samples.kotlin"
        minSdk 21
        targetSdk 31
        versionCode Integer.valueOf(System.getenv("CIRCLE_BUILD_NUM") ?: 1)
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "APPCUES_ACCOUNT_ID", "\"PLACEHOLDER_ACCOUNT_ID\""
        buildConfigField "String", "APPCUES_APPLICATION_ID", "\"PLACEHOLDER_APPLICATION_ID\""
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    /**
     * 1. run gradlew appcues:publishToMavenLocal
     * 2. replace the implementation project(":appcues") with the versioned implementation below.
     * The `changing = true` forces gradle to reload dependency.
     */
    implementation project(":appcues")
//    implementation('com.appcues:appcues-debug:0.1.0') {
//        changing = true
//    }

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
}

task versionTxt()  {
    doLast {
        new File(projectDir.parentFile.parentFile, "sample_version.txt").text = "$android.defaultConfig.versionCode ($android.defaultConfig.versionName)"
    }
}
